Using System
Using System.Data
Using System.Configuration
Using System.Web
Using System.Web.Security
Using System.Web.UI
Using System.Web.UI.WebControls
Using System.Web.UI.WebControls.WebParts
Using System.Web.UI.HtmlControls
Using System.Net.Mail 

/// <summary>
/// Summary description for Email
/// </summary>
BegClass ErrorManager Access(*Public)

    DclConst CRLF  Value( U'000D000A' )  Access( *Public ) 

    BegSr SendAdminErrorEmail Access( *Public ) 
        DclSrParm ErrorBody  Type( *String )
    
        DclFld MailObj       Type( SmtpClient ) 
        DclFld MailServer    Type( *String ) 
        DclFld ToAddress     Type( *String ) 
        DclFld FromAddress   Type( *String ) 
        DclFld Subject       Type( *String ) 
        DclFld MyMessage     Type( MailMessage ) New()
        DclFld BodyWithStyle Type( System.Text.StringBuilder ) New( 512 ) 

        MailServer   = ConfigurationSettings.AppSettings( "MailServer" ) 
        Subject      = "An error occurred in Web app: " ++
                       ConfigurationSettings.AppSettings( "WebAppName" )         
        ToAddress    = ConfigurationSettings.AppSettings( "AdminEmail" ) 
        FromAddress  = ConfigurationSettings.AppSettings( "AdminEmail" ) 

        MailObj = *New SmtpClient( MailServer ) 

        MyMessage.IsBodyHtml = *True 

        MyMessage.To.Add( ToAddress ) 
        MyMessage.From    = *New MailAddress( FromAddress ) 
        MyMessage.Subject = Subject
        
        
        BodyWithStyle.Append( "<style>body { font-family: Verdana, Tahoma, Sans-Serif; } </style>" ) 
        BodyWithStyle.Append( Subject + "<br>" ) 
        BodyWithStyle.Append( ErrorBody ) 
        BodyWithStyle.Replace( CRLF, "<br>" ) 
        
        MyMessage.Body = BodyWithStyle.ToString() 

        Try         
            MailObj.Send( MyMessage )
        Catch e1 System.Exception
            Throw e1
        EndTry                    
    EndSr    
    
    BegFunc GetErrorDetails Type( *String )  Access( *Public ) 
        //
        // Get all error message and stack trace info for a given error.
        //
        DclSrParm Error    Type( System.Exception ) 
        DclSrParm NewLine  Type( *String  ) 
        
        // NewLine provides the way new lines should be formed. When the
        // text is used inside a multi-line textbox, a CRLF should be used 
        // as a newline. When the text is sent as a formatted email (as 
        // an HTML document) the <br> tag should be used.

        DclFld ErrorDetails Type( System.Text.StringBuilder ) New( 512 ) 
        DclFld Level        Type( *Integer4 ) 
        DclFld Now          Type( DateTime ) 
        
        Now = DateTime.Now 
        ErrorDetails.Append( "Error occurred at: " ++
                              Now.ToString( "dddd, MMMM dd, yyyy" ) ++
                              " at " + Now.ToString( "hh:mm:ss t" ) ++
                              "M" + NewLine + NewLine ) 

        AddErrorDetail( Error, ErrorDetails, Level, NewLine ) 
        
        Error = Error.InnerException
        DoWhile ( Error <> *Nothing ) 
            Level = Level + 1
            AddErrorDetail( Error, ErrorDetails, Level, NewLine ) 
            Error = Error.InnerException
        EndDo 
        
        LeaveSr ErrorDetails.ToString() 
    EndFunc 
    
    BegSr AddErrorDetail
        DclSrParm Error         Type( System.Exception ) 
        DclSrParm ErrorDetails  Type( System.Text.StringBuilder )
        DclSrParm Level         Type( *Integer4 )
        DclSrParm NewLine       Type( *String  ) 
        
        ErrorDetails.Append( "Error level " + Level.ToString() + NewLine )              
        ErrorDetails.Append( "Error message: " )
        ErrorDetails.Append( Error.Message + NewLine + NewLine ) 
        ErrorDetails.Append( "Stack trace:" + NewLine ) 
        ErrorDetails.Append( Error.StackTrace + NewLine + NewLine + NewLine + NewLine ) 
    EndSr
         

EndClass

BegClass WarningError Access( *Public ) 
    DclFld _Text Type( *String )
    BegProp Text Type( *String )  Access( *Public )
        BegGet
            LeaveSr Value( _Text )
        EndGet
        BegSet
            _Text = *PropVal
        EndSet
    EndProp

    DclFld _Url Type( *String ) 
    BegProp Url Type( *String )  Access( *Public )
        BegGet
            LeaveSr Value( _Url )
        EndGet
        BegSet
            _Url = *PropVal
        EndSet
    EndProp
    
    DclFld _UrlText Type( *String )
    BegProp UrlText Type( *String )  Access( *Public )
        BegGet
            LeaveSr Value( _UrlText )
        EndGet
        BegSet
            _UrlText = *PropVal
        EndSet
    EndProp

    BegConstructor Access( *Public ) 
    EndConstructor
EndClass




